{-

  TASK 4 - (De)Serialize to Cell
  Write the methods
    a) for serialization of tuple of arbitrary values of different types to Cell
    b) for deserialization of Cell created by method above to original tuple

  `serialize` method gets as input tuple with arbitrary number of elements from 0
  to 128 (both 0 and 128 elements are allowed) and outputs Cell. Elements of the
  tuple may be `null`, `int`, `cell`, `slice`, `tuple` (with the same limitations
  as original tuple). It is guaranteed that the maximum nesting level of tuples
  is less than 4 and total number of elements less than 1024.
  `deserialize` method gets a cell produced by `serialize` method and should
  return origin tuple.

  Note, for illustrative purposes`serialize_t3` and `deserialize_t3` functions
  which serialize and deserialize tuple with exactly 3 elements
  (only null, int, cell and slice are supportd) to/from a cell  have been added.
  Participants are free to not use logic from there and come up with their own
  implementations.

  Tests will only check that test_tuple == deserialize(serialize(test_tuple)).
  Intermediate representation can be arbitrary.
-}


() recv_internal() {
}

;; testable
(cell) serialize(tuple values) method_id {
  builder b = begin_cell();
  if (tuple_length(values) == 0) {
    return b.store_uint(5,3).end_cell();
  }
  var x = values~tpop();
  if(is_null(x)) {
    b~store_uint(0,3);
  }
  if(is_int(x)) {
    int i_x = force_cast_to_int(x);
    b = b.store_uint(1,3).store_int(i_x, 257);
  }
  if(is_cell(x)) {
    cell c_x = force_cast_to_cell(x);
    b = b.store_uint(2,3).store_ref(c_x);
  }
  if(is_slice(x)) {
    slice s_x = force_cast_to_slice(x);
    b = b.store_uint(3,3).store_ref(begin_cell().store_slice(s_x).end_cell());
  }
  if(is_tuple(x)) {
    tuple s_x = force_cast_to_tuple(x);
    b = b.store_uint(4,3).store_ref(serialize(s_x));
  }
  return b.store_ref(serialize(values)).end_cell();
}

;; testable
(tuple) deserialize(cell serialized) method_id {
  slice s = serialized.begin_parse();
  tuple t = empty_tuple();
  int element_type = s~load_uint(3);
  if(element_type == 0) {
    t~tpush(null());
  }
  if(element_type == 1) {
    t~tpush(s~load_int(257));
  }
  if(element_type == 2) {
    t~tpush(s~load_ref());
  }
  if(element_type == 3) {
    t~tpush(s~load_ref().begin_parse());
  }
  if(element_type == 4) {
    t~tpush(deserialize(s~load_ref()));
  }
  if(element_type == 5) {
    return empty_tuple();
  }
  tuple tt = deserialize(s~load_ref());
  tt~tpush(t~tpop());
  return tt;
}
